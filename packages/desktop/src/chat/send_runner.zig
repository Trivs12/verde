//! UI-independent provider send runner used by daemon-owned chat turns.

const std = @import("std");
const workspace_folders = @import("../workspace/folders.zig");
const harness = @import("../providers/harness.zig");

const log = std.log.scoped(.chat_send_runner);

pub const FastMode = enum { off, on };
pub const AccessMode = enum { supervised, full_access };

pub const Request = struct {
    provider: harness.Provider,
    harness_kind: harness.HarnessKind,
    project_path: []const u8,
    /// Per-thread working-directory override; null follows `project_path`.
    cwd: ?[]const u8 = null,
    prompt: []const u8,
    image_paths: []const []const u8 = &.{},
    provider_thread_id: ?[]const u8 = null,
    thread_title: []const u8 = "",
    model_ref: ?[]const u8 = null,
    reasoning_effort: ?harness.ReasoningEffort = null,
    opencode_reasoning_variant: ?[]const u8 = null,
    cursor_model_params_json: ?[]const u8 = null,
    fast_mode: FastMode = .off,
    access_mode: AccessMode = .full_access,
};

pub const Result = struct {
    provider_thread_id: []const u8,
    reply_text: []const u8,
};

pub const Sink = struct {
    context: ?*anyopaque = null,
    on_thread_id: ?*const fn (?*anyopaque, []const u8) void = null,
    on_turn_id: ?*const fn (?*anyopaque, []const u8) void = null,
    on_stream_delta: ?*const fn (?*anyopaque, []const u8) void = null,
    on_stream_event: ?*const fn (?*anyopaque, harness.StreamEvent) void = null,
    on_answer_ready: ?*const fn (?*anyopaque, []const u8) void = null,
    on_failure: ?*const fn (?*anyopaque, []const u8) void = null,
    on_should_stop: ?*const fn (?*anyopaque) bool = null,
    on_approval_request: ?*const fn (?*anyopaque, harness.ApprovalRequest) harness.ApprovalDecision = null,
};

pub fn run(allocator: std.mem.Allocator, request: Request, sink: Sink) !Result {
    if (request.harness_kind != .local_cli) return error.UnsupportedHarnessMode;

    var workspace = try workspace_folders.resolve(allocator, request.project_path);
    defer workspace.deinit();
    if (workspace.configured) try workspace_folders.sync(allocator, request.project_path);
    const requested_cwd = request.cwd orelse request.project_path;
    const request_cwd = if (std.mem.eql(u8, requested_cwd, request.project_path)) workspace.cwd else requested_cwd;
    const contextual_prompt = if (workspace.configured)
        try std.fmt.allocPrint(allocator, "{s}\n{s}", .{ request.prompt, workspace.context })
    else
        null;
    defer if (contextual_prompt) |value| allocator.free(value);
    // Muse's supervised sandbox only supports one writable root. Preserve its
    // access policy instead of silently disabling the sandbox for extra folders.
    if (request.provider == .muse and request.access_mode == .supervised and workspace.roots.len > 1) {
        if (sink.on_failure) |on_failure| on_failure(sink.context, "Muse in Supervised mode supports only one writable folder. Choose another provider for this multi-folder workspace.");
        return error.ProviderDoesNotSupportAdditionalWorkspaceRoots;
    }

    const provider_config = switch (request.provider) {
        .opencode => harness.ProviderConfig{ .opencode = .{
            .allocator = allocator,
            .working_directory = request_cwd,
            .launch_if_missing = true,
        } },
        .codex => harness.ProviderConfig{ .codex = .{
            .cwd = request_cwd,
            .launch_on_connect = true,
        } },
        .claude => harness.ProviderConfig{ .claude = .{ .cwd = request_cwd } },
        .cursor => harness.ProviderConfig{ .cursor = .{ .cwd = request_cwd, .model = request.model_ref } },
        .pi => harness.ProviderConfig{ .pi = .{ .cwd = request_cwd } },
        .fx => harness.ProviderConfig{ .fx = .{ .cwd = request_cwd, .model = request.model_ref } },
        .grok => harness.ProviderConfig{ .grok = .{ .cwd = request_cwd, .model = request.model_ref } },
        .muse => harness.ProviderConfig{ .muse = .{ .cwd = request_cwd, .model = request.model_ref } },
    };

    log.info("send starting provider={s} cwd={s} model_len={d} thread_id_len={d} prompt_len={d}", .{
        @tagName(request.provider),
        request_cwd,
        if (request.model_ref) |model| model.len else 0,
        if (request.provider_thread_id) |thread_id| thread_id.len else 0,
        request.prompt.len,
    });

    var client = try harness.connect(allocator, provider_config);
    defer client.deinit();

    const image_attachments = try allocator.alloc(harness.types.ImageAttachment, request.image_paths.len);
    defer allocator.free(image_attachments);
    for (request.image_paths, 0..) |path, index| image_attachments[index] = .{ .path = path };

    const send_result = try client.sendPrompt(allocator, .{
        .thread_id = request.provider_thread_id,
        .thread_title = request.thread_title,
        .prompt = contextual_prompt orelse request.prompt,
        .workspace_roots = if (workspace.folders.len > 0) workspace.roots else &.{},
        .image = if (request.image_paths.len > 0) .{ .path = request.image_paths[0] } else null,
        .images = image_attachments,
        .cwd = request_cwd,
        .model = request.model_ref,
        .opencode_variant = if (request.provider == .opencode) request.opencode_reasoning_variant else null,
        .cursor_model_params_json = if (request.provider == .cursor) request.cursor_model_params_json else null,
        .reasoning_effort = if (request.provider == .opencode and request.opencode_reasoning_variant != null) null else request.reasoning_effort,
        .service_tier = serviceTierForMode(request.provider, request.fast_mode),
        .approval_policy = approvalPolicyForMode(request.access_mode),
        .sandbox_mode = sandboxModeForMode(request.provider, request.access_mode),
        .stream_context = sink.context,
        .on_thread_id = sink.on_thread_id,
        .on_turn_id = sink.on_turn_id,
        .on_stream_delta = sink.on_stream_delta,
        .on_stream_event = sink.on_stream_event,
        .on_answer_ready = sink.on_answer_ready,
        .on_failure = sink.on_failure,
        .on_should_stop = sink.on_should_stop,
        .on_approval_request = sink.on_approval_request,
    });

    return .{ .provider_thread_id = send_result.thread_id, .reply_text = send_result.reply_text };
}

/// Dynamic model catalog for daemon clients (web/CLI). Connects without
/// launching provider servers, so it only reports models while the provider
/// is reachable (OpenCode server running, cursor-agent/claude CLI installed);
/// callers keep their static fallback tables otherwise.
pub fn listModels(
    allocator: std.mem.Allocator,
    provider: harness.Provider,
    project_path: []const u8,
) ![]harness.ModelInfo {
    const provider_config = switch (provider) {
        .opencode => harness.ProviderConfig{ .opencode = .{
            .allocator = allocator,
            .working_directory = project_path,
            .launch_if_missing = false,
        } },
        // Codex has no model-discovery RPC; clients use the static table.
        .codex => return error.UnsupportedOperation,
        .claude => harness.ProviderConfig{ .claude = .{ .cwd = project_path } },
        .cursor => harness.ProviderConfig{ .cursor = .{ .cwd = project_path } },
        .pi => harness.ProviderConfig{ .pi = .{ .cwd = project_path } },
        .fx => harness.ProviderConfig{ .fx = .{ .cwd = project_path } },
        .grok => harness.ProviderConfig{ .grok = .{ .cwd = project_path } },
        .muse => harness.ProviderConfig{ .muse = .{ .cwd = project_path } },
    };
    var client = try harness.connect(allocator, provider_config);
    defer client.deinit();
    return client.listModels(allocator);
}

/// Construct a provider client for daemon-owned metadata and command RPCs.
/// Callers select whether operations such as imports/slash commands may start
/// a missing provider server; readiness probes always pass false.
pub fn connectProvider(
    allocator: std.mem.Allocator,
    provider: harness.Provider,
    project_path: []const u8,
    launch_if_missing: bool,
) !harness.ProviderClient {
    const provider_config: harness.ProviderConfig = switch (provider) {
        .opencode => .{ .opencode = .{
            .allocator = allocator,
            .working_directory = project_path,
            .launch_if_missing = launch_if_missing,
        } },
        .codex => .{ .codex = .{
            .cwd = project_path,
            .launch_on_connect = launch_if_missing,
        } },
        .claude => .{ .claude = .{ .cwd = project_path } },
        .cursor => .{ .cursor = .{ .cwd = project_path } },
        .pi => .{ .pi = .{ .cwd = project_path } },
        .fx => .{ .fx = .{ .cwd = project_path } },
        .grok => .{ .grok = .{ .cwd = project_path } },
        .muse => .{ .muse = .{ .cwd = project_path } },
    };
    return harness.connect(allocator, provider_config);
}

fn approvalPolicyForMode(mode: AccessMode) ?harness.ApprovalPolicy {
    return switch (mode) {
        .full_access => .never,
        .supervised => .on_request,
    };
}

fn serviceTierForMode(provider: harness.Provider, fast_mode: FastMode) ?harness.ServiceTier {
    if (provider != .codex) return null;
    return switch (fast_mode) {
        .on => .fast,
        .off => null,
    };
}

fn sandboxModeForMode(provider: harness.Provider, mode: AccessMode) ?harness.SandboxMode {
    if (provider != .codex and provider != .claude and provider != .muse) return null;
    return switch (mode) {
        .full_access => .danger_full_access,
        .supervised => .workspace_write,
    };
}
